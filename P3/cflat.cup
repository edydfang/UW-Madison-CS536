/**********************************************************************
 Java CUP specification for a parser for cflat programs
 **********************************************************************/

import java_cup.runtime.*;
import java.util.*;

/* The code below redefines method syntax_error to give better error messages
 * than just "Syntax error"
 */
parser code {:

public void syntax_error(Symbol currToken) {
    if (currToken.value == null) {
        ErrMsg.fatal(0,0, "Syntax error at end of file");
    }
    else {
        ErrMsg.fatal(((TokenVal)currToken.value).linenum,
                     ((TokenVal)currToken.value).charnum,
                     "Syntax error");
    }
    System.exit(-1);
}
:};


/* Terminals (tokens returned by the scanner) */
terminal                INT;
terminal                BOOL;
terminal                VOID;
terminal TokenVal     TRUE;
terminal TokenVal       FALSE;
terminal                STRUCT;
terminal                CIN;
terminal                COUT;
terminal                IF;
terminal                ELSE;
terminal                WHILE;
terminal		REPEAT;
terminal                RETURN;
terminal IdTokenVal     ID;
terminal IntLitTokenVal INTLITERAL;
terminal StrLitTokenVal STRINGLITERAL;
terminal                LCURLY;
terminal                RCURLY;
terminal                LPAREN;
terminal                RPAREN;
terminal                SEMICOLON;
terminal                COMMA;
terminal                DOT;
terminal                WRITE;
terminal                READ;
terminal                PLUSPLUS;
terminal                MINUSMINUS;
terminal                PLUS;
terminal                MINUS;
terminal                TIMES;
terminal                DIVIDE;
terminal                NOT;
terminal                AND;
terminal                OR;
terminal                EQUALS;
terminal                NOTEQUALS;
terminal                LESS;
terminal                GREATER;
terminal                LESSEQ;
terminal                GREATEREQ;
terminal                ASSIGN;


/* Nonterminals
 *
 * NOTE: You will need to add more nonterminals to this list as you
 *       add productions to the grammar below.
 */
non terminal ProgramNode      program;
non terminal LinkedList       declList;
non terminal DeclNode         decl;
non terminal VarDeclNode      varDecl;
non terminal TypeNode         type;
non terminal IdNode           id;
non terminal FnDeclNode       fnDecl; 
non terminal FormalsListNode    formals;
non terminal LinkedList  formalsList;
non terminal FnBodyNode fnBody;
non terminal FormalDeclNode formalDecl;
non terminal LinkedList stmtList;
non terminal StmtNode stmt;
non terminal AssignNode assignExp;
non terminal CallExpNode fncall;
non terminal ExpNode loc;
non terminal LinkedList  actualList;
non terminal ExpNode exp;
non terminal ExpNode term;
non terminal LinkedList varDeclList;
non terminal StructDeclNode structDecl;
non terminal LinkedList structBody;


 
/* NOTE: Add precedence and associativity declarations here */
precedence left DOT, TIMES, MINUS, PLUS, OR, AND, DIVIDE;
precedence right ASSIGN;
precedence nonassoc GREATER, LESSEQ, GREATEREQ, LESS, NOTEQUALS, EQUALS;

start with program;


/* Grammar with actions
 *
 * NOTE: add more grammar rules below
 */
program         ::= declList: d
                {: RESULT = new ProgramNode(new DeclListNode(d));
                :}
                ;

declList        ::= declList:dl decl:d
                {: dl.addLast(d);
                   RESULT = dl;
                :}
                | /* epsilon */
                {: RESULT = new LinkedList<DeclNode>();
                :}
                ;

decl            ::= varDecl:v
                {: RESULT = v;
                :}
                | fnDecl:fn
                {: RESULT = fn;
                :}
                | structDecl:s
                {: RESULT = s;
                :}
                ;

varDeclList     ::= varDeclList:vl varDecl:v
                {: vl.addLast(v);
                   RESULT = vl;
                :}
                | /* epsilon */
                {: RESULT = new LinkedList<VarDeclNode>();
                :}
                ;

varDecl         ::= type:t id:i SEMICOLON
                {: RESULT = new VarDeclNode(t, i, VarDeclNode.NOT_STRUCT);
                :}
                ;

fnDecl          ::= type:t id:i formals:fms fnBody:fnbd
                {: RESULT = new FnDeclNode(t, i, fms, fnbd);
                :}
                ;

structDecl      ::= STRUCT id:i LCURLY structBody:sb RCURLY SEMICOLON
                {: RESULT = new StructDeclNode(i, new DeclListNode(sb));
                :}
                ;
                
structBody      ::= structBody:sb varDecl:v
                {: sb.addLast(v);
                RESULT = sb;
                :}
                | varDecl:v
                {: LinkedList ll = new LinkedList<VarDeclNode>();
                  ll.addLast(v);
                  RESULT = ll;
                :}
                ;

formals         ::= LPAREN RPAREN
                {: RESULT = new FormalsListNode(new LinkedList<FormalDeclNode>());
                :}
                | LPAREN formalsList:fmsl RPAREN
                {: RESULT = new FormalsListNode(fmsl);
                :}
                ;

formalsList     ::= formalDecl:fd
                {:  LinkedList ll = new LinkedList<FormalDeclNode>();
                    ll.addLast(fd);
                    RESULT = ll;
                :}
                | formalDecl:fd COMMA formalsList:fl
                {:  fl.addLast(fd); 
                    RESULT = fl;
                :}
                ;

/* // note: no struct parameters */
formalDecl      ::= type:t id:i        
                {: RESULT = new FormalDeclNode(t, i);
                :}
                ;
/* may have some problem */
fnBody          ::= LCURLY varDeclList:dl stmtList:sl RCURLY 
                {: 
                RESULT = new FnBodyNode(new DeclListNode(dl), new StmtListNode(sl));
                :}
                ;

stmtList        ::= stmtList:sl stmt:s
                {:  sl.addLast(s);
                RESULT = sl;
                :}
                | /* epsilon */
                {:  RESULT = new LinkedList<StmtNode>();
                :}
                ;

stmt            ::= assignExp:a SEMICOLON
                {: RESULT = new AssignStmtNode(a);
                :}
                | loc:l PLUSPLUS SEMICOLON
                {: RESULT = new PostIncStmtNode(l);
                :}
                | loc:l MINUSMINUS SEMICOLON
                {: RESULT = new PostDecStmtNode(l);
                :}
                | CIN READ loc:l SEMICOLON
                {: RESULT = new ReadStmtNode(l);
                :}
                | COUT WRITE exp:e SEMICOLON
                {: RESULT = new WriteStmtNode(e);
                :}
                | IF LPAREN exp:e RPAREN LCURLY varDeclList:dl stmtList:sl RCURLY
                {: RESULT = new IfStmtNode(e, new DeclListNode(dl), new StmtListNode(sl));
                :}
                | IF LPAREN exp:e RPAREN LCURLY varDeclList:dl1 stmtList:sl1 RCURLY ELSE LCURLY varDeclList:dl2 stmtList:sl2 RCURLY
                {: RESULT = new IfElseStmtNode(e, new DeclListNode(dl1), new StmtListNode(sl1), new DeclListNode(dl2), new StmtListNode(sl2));
                :}
                | WHILE LPAREN exp:e RPAREN LCURLY varDeclList:dl stmtList:sl RCURLY
                {:  RESULT = new WhileStmtNode(e, new DeclListNode(dl), new StmtListNode(sl));
                :}
		        | REPEAT LPAREN exp:e RPAREN LCURLY varDeclList:dl stmtList:sl RCURLY
                {: RESULT = new RepeatStmtNode(e, new DeclListNode(dl), new StmtListNode(sl));
                :}
                | RETURN exp:e SEMICOLON
                {: RESULT = new ReturnStmtNode(e);
                :}
                | RETURN SEMICOLON
                {: RESULT = new ReturnStmtNode(null);
                :}
                | fncall:fc SEMICOLON
                {: RESULT = new CallStmtNode(fc);
                :}
                ;

assignExp       ::= loc:l ASSIGN exp:e
                {: RESULT = new AssignNode(l, e);
                :}
                ;
                
exp             ::= assignExp:a
                {: RESULT = a;
                :}
                | exp:e1 PLUS exp:e2
                {: RESULT = new PlusNode(e1, e2);
                :}
                | exp:e1 MINUS exp:e2
                {: RESULT = new MinusNode(e1, e2);
                :}
                | exp:e1 TIMES exp:e2
                {: RESULT = new TimesNode(e1, e2);
                :}
                | exp:e1 DIVIDE exp:e2
                {: RESULT = new DivideNode(e1, e2);
                :}
                | NOT exp:e
                {: RESULT = new NotNode(e);
                :}
                | exp:e1 AND exp:e2 
                {: RESULT = new AndNode(e1, e2);
                :}
                | exp:e1 OR exp:e2
                {: RESULT = new OrNode(e1, e2);
                :}
                | exp:e1 EQUALS exp:e2
                {: RESULT = new EqualsNode(e1, e2);
                :}
                | exp:e1 NOTEQUALS exp:e2
                {: RESULT = new NotEqualsNode(e1, e2);
                :}
                | exp:e1 LESS exp:e2
                {: RESULT = new LessNode(e1, e2);
                :}
                | exp:e1 GREATER exp:e2
                {: RESULT = new GreaterNode(e1, e2);
                :}
                | exp:e1 LESSEQ exp:e2
                {: RESULT = new LessEqNode(e1, e2);
                :}
                | exp:e1 GREATEREQ exp:e2
                {: RESULT = new GreaterEqNode(e1, e2);
                :}
                | MINUS term:t
                {: RESULT = new UnaryMinusNode(t);
                :}
                | term:t
                {: RESULT = t; 
                :}
                ;

term            ::= loc:l
                {: RESULT = l;
                :}
                | INTLITERAL:i
                {: RESULT = new IntLitNode(i.linenum, i.charnum, i.intVal);
                :}
                | STRINGLITERAL:s
                {:
                   RESULT = new StringLitNode(s.linenum, s.charnum, s.strVal);
                :}
                | TRUE:t
                {: RESULT = new TrueNode(t.linenum, t.charnum);
                :}
                | FALSE:f
                {: RESULT = new FalseNode(f.linenum, f.charnum);
                :}
                | LPAREN exp:e RPAREN
                {: RESULT = e;
                :}
                | fncall:f
                {: RESULT = f;
                :}
                ;

fncall          ::=  id:i LPAREN RPAREN   // fn call with no args
                {: RESULT = new CallExpNode(i);
                :}
                | id:i LPAREN actualList:al RPAREN  // with args
                {: RESULT = new CallExpNode(i, new ExpListNode(al));
                :}
                ;

actualList      ::= exp:e
                {:  LinkedList ll = new LinkedList<ExpNode>();
                    ll.addLast(e);
                    RESULT = ll;
                :}
                | actualList:ll COMMA exp:e
                {:
                    ll.addLast(e);
                    RESULT = ll;
                :}
                ;

loc             ::= id:i
                {: RESULT = i;
                :}
                | loc:l DOT id:i
                {: RESULT = new DotAccessExpNode(l, i);
                :}
                ;


type            ::= INT
                {: RESULT = new IntNode();
                :}
                | BOOL
                {: RESULT = new BoolNode();
                :}
                | VOID
                {: RESULT = new VoidNode();
                :}
                ;

id              ::= ID:i
                {: RESULT = new IdNode(i.linenum, i.charnum, i.idVal);
                :}
                ;
				
